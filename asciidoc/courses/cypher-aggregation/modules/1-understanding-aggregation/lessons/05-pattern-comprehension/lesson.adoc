= Using Pattern Comprehension
:type: quiz
:sandbox: true

//[.video]
//video::dyAJ_H1eixk[youtube,width=560,height=315]

//https://youtu.be/dyAJ_H1eixk

[.transcript]

== Using pattern comprehension

Pattern comprehension in Cypher enables you to create a list based upon values obtained during a query.

Here is how we aggregate using collections:

[source,cypher]
----
PROFILE
MATCH (a:Person)-[:ACTED_IN]-(m:Movie)-[:DIRECTED]-(d:Person)
WHERE m.year = 2015
RETURN m.title AS Movie, collect(DISTINCT a.name) AS Actors, collect (DISTINCT d.name) AS Directors
----

Using Cypher's pattern comprehension syntax: `[<pattern> | value]`, we dynamically return a list using values obtained from the pattern.
In general, pattern comprehension performs better than using `collect()`.

Here is the same query using pattern comprehension:

[source,cypher]
----
PROFILE
MATCH (m:Movie)
WHERE m.year = 2015
RETURN m.title AS Movie,
[(dir:Person)-[:DIRECTED]->(m) | dir.name] AS Directors,
[(actor:Person)-[:ACTED_IN]->(m) | actor.name] AS Actors
----

Notice that the first query returns 178 rows and the second query returns 180 rows.
This is because there are two movies that have no directors in the graph.

=== Filtering during pattern comprehension

You can specify a filtering criteria for the pattern:

[source,cypher]
----
MATCH (a:Person {name: 'Tom Hanks'})
RETURN [(a)--(b:Movie)
WHERE b.title CONTAINS "Toy" | b.title ]
AS Movies
----

=== Adding more context to the list returned

You can even provide more context to the elements of the list returned where we add the year to the movie title for each element of the collection:

[source,cypher]
----
MATCH (a:Person {name: 'Tom Hanks'})
RETURN [(a)--(b:Movie)
WHERE b.title CONTAINS "Toy" | b.title + ": " + b.year]
AS Movies
----

== Check your understanding

include::questions/1-pattern-comprehension.adoc[leveloffset=+1]
include::questions/2-pattern-comprehension.adoc[leveloffset=+1]

[.summary]
== Summary

In this lesson, you reviewed and learned more about aggregating data  pattern comprehension your Cypher queries.
In the next Challenge, you will create a query  to aggregate using pattern comprehension.
