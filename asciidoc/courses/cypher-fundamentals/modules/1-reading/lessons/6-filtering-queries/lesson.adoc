= Filtering queries
:type: quiz
:order: 6

[.transript]
Earlier, you learned that the `WHERE` clause is used to tell the query engine to filter what nodes are retrieved from the graph.
There are many ways that you can filter what is retrieved.
In this lesson you will learn about some of the ways that you can filter your queries.

You have already learn how you can test equality for properties of a node and how you can use logical expressions to further filter what you want to retrieve.

For example, this query retrieves the Person nodes and Movie nodes where the person acted in a movie that was released in 2008 or 2009:

[source,cypher, role = noplay]
----
MATCH (p:Person)-[:ACTED_IN]->(m:Movie)
WHERE m.released = 2008 OR m.released = 2009
RETURN p, m
----

== Filtering by node labels

You have already seen this type of query. It returns the names of all people who acted in the movie, The Matrix.

[source,cypher, role = noplay]
----
MATCH (p:Person)-[:ACTED_IN]->(m:Movie)
WHERE m.title='The Matrix'
RETURN p.name
----

An alternative to this query is the following where we test the node labels in the `WHERE` clause:

[source,cypher, role = noplay]
----
MATCH (p)-[:ACTED_IN]->(m)
WHERE p:Person AND m:Movie AND m.title='The Matrix'
RETURN p.name
----

Both queries execute the same way, but you may want to use one style of filtering over another in your code.

== Filtering using ranges

You can specify a range for filtering a query. Here we want to retrieve  Person nodes of people who acted in movies released between 2000 and 2005:

[source,cypher, role = noplay]
----
MATCH (p:Person)-[:ACTED_IN]->(m:Movie)
WHERE 2000 <= m.released <= 2003
RETURN p.name, m.title, m.released
----

== Filtering by existence of a property

Recall that there is no requirement that a node or relationship has a given property.
Here is an example of a query where we only want to return Movie nodes where Jack Nicholson acted in and have the _tagline_ property.

[source,cypher, role = noplay]
----
MATCH (p:Person)-[:ACTED_IN]->(m:Movie)
WHERE p.name='Jack Nicholson' AND m.tagline IS NOT NULL
RETURN m.title, m.tagline
----

== Filtering by partial strings

Cypher has a set of string-related keywords that you can use in your `WHERE` clauses to test string property values.
You can specify `STARTS WITH`, `ENDS WITH`, and `CONTAINS`.

For example, to find all actors in the graph whose first name is _Michael_, you would write:

[source,cypher, role = noplay]
----
MATCH (p:Person)-[:ACTED_IN]->()
WHERE p.name STARTS WITH 'Michael'
RETURN p.name
----

String tests are case-sensitive so you may need to use the toLower() or toUpper() functions to ensure the test yields the correct results.
For example:

[source,cypher, role = noplay]
----
MATCH (p:Person)-[:ACTED_IN]->()
WHERE toLower(p.name) STARTS WITH 'michael'
RETURN p.name
----

==  Filtering by patterns in the graph

Suppose you wanted to find all people who wrote a movie but did not direct that same movie.
Here is how you would perform the query:

[source,cypher, role = noplay]
----
MATCH (p:Person)-[:WROTE]->(m:Movie)
WHERE NOT exists( (p)-[:DIRECTED]->(m) )
RETURN p.name, m.title
----

== Filtering using lists

If you have a set of values you want to test with, you can place them in a list or you can test with an existing list in the graph.
A Cypher list is a comma-separated set of values within square brackets.

You can define the list in the `WHERE` clause.
During the query, the graph engine will compare each property with the values `IN` the list.
You can place either numeric or string values in the list, but typically, elements of the list are of the same type of data.
If you are testing with a property of a string type, then all the elements of the list will be strings.

In this example, we only want to retrieve _Person_ nodes of people born in 1965, 1970, or 1975:

[source,cypher, role = noplay]
----
MATCH (p:Person)
WHERE p.born IN [1965, 1970, 1975]
RETURN p.name, p.born
----

You can also compare a value to an existing list in the graph.

We know that the _:ACTED_IN_ relationship has a property, _roles_ that contains the list of roles an actor had in a particular movie they acted in.
Here is the query we write to return the name of the actor who played _Neo_ in the movie _The Matrix_:

[source,cypher, role = noplay]
----
MATCH (p:Person)-[r:ACTED_IN]->(m:Movie)
WHERE  'Neo' IN r.roles AND m.title='The Matrix'
RETURN p.name, r.roles
----

== Check your understanding

include::questions/1-complete-query.adoc[]

include::questions/2-seventiespeople.adoc[]

[.summary]
== Summary

In this lesson, you learned some of the ways you can filter what is retrieved from the graph.
In the next challenge, you will demonstrate your skills at filtering nodes retrieved.

