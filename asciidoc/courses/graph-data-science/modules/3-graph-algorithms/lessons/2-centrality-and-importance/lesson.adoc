= Centrality and Importance
:type: quiz

[.video]
video::xxxx[youtube,width=560,height=315]


[.transcript]
== Introduction
Centrality algorithms are used to determine the importance of distinct nodes in a network.

Common use cases of centrality include:

* *Recommendations:* recommending content or products by finding the most influential piece of content or product offering in your catalog
* *Supply chain analytics:* finding the most important node in your supply chain, whether it be a supplier in a network, a raw material that is part of a manufactured product, or a port in a route
* *Fraud & Anomaly Detection*: Finding users with many shared identifiers in a system or who otherwise act as a bridge between many communities.

=== Degree Centrality
Degree centrality is one of the most ubiquitous and simple centrality algorithms.  It calculates the number of relationships a node has. In the GDS implementation, we specifically calculate 'out-degree' centrality which is the count of outgoing relationships from a node. Below is an example of using degree centrality to count the number of movies each actor has acted in.

----
// include to clean up if there is a previous 'proj' graph
CALL gds.graph.drop('proj');
//projection of actors acting in movies
CALL gds.graph.project('proj', ['Actor','Movie'], 'ACTED_IN');
----

----
//get top 5 most prolific actors (those in the most movies)
//using degree centrality which counts number of `ACTED_IN` relationships
CALL gds.degree.stream('proj')
YIELD nodeId, score
RETURN gds.util.asNode(nodeId).name AS actorName, score AS numberOfMoviesActedIn
ORDER BY numberOfMoviesActedIn DESCENDING, actorName LIMIT 5
----


=== PageRank
Another common centrality algorithm is PageRank. PageRank was originally developed by Google co-founders Larry Page and Sergey Brin at Stanford University in 1996 as part of a research project about a new kind of search engine. It has since been used by Google Search to rank web pages in their search engine results.

In its initial design, PageRank estimates the importance of webpages in a graph where nodes represent webpages and relationships represents links between them. It accomplishes this by counting the number of links to a page weighted by the importance of the linking webpage. The underlying assumption is that more important webpages are likely to have more incoming relationships from other import webpages.

//[perhaps an equation here]

While pageRank was initially designed for graphs of the web, it can be applied to measure the importance of nodes in other types of graphs, such as users in social networks, or accounts in payment and money transfer platforms. Below is an example of applying pageRank to find the most influential persons in the director & actor network of recent high grossing movies.

----
//drop last graph projection
CALL gds.graph.drop('proj');

//create Cypher projection for network of people directing actors
//filter to recent high grossing movies
CALL gds.graph.project.cypher(
  'proj',
  'MATCH(a:Person) RETURN id(a) AS id, labels(a) AS labels',
  'MATCH(a1:Person)-[:DIRECTED]->(m:Movie)<-[:ACTED_IN]-(a2)
   WHERE m.year >= 1990 AND m.revenue >= 10000000
   RETURN id(a1) AS source , id(a2) AS target, count(*) AS actedWithCount,
    "DIRECTED_ACTOR" AS type'
);
----

----
//top 5 most influential people in director-actor network Using PageRank
CALL gds.pageRank.stream('proj-cypher')
YIELD nodeId, score
RETURN gds.util.asNode(nodeId).name AS personName, score AS influence
ORDER BY influence DESCENDING, personName LIMIT 5
----
=== Other Centrality algorithms
Other GDS production tier centrality algorithms include:

* *Betweenness Centrality*: Measures the amount of `influence` a node has over the flow of information in a graph. It is often used to find nodes that serve as a bridge from one part of a graph to another.
* *Eigenvector Centrality*: Measures the transitive influence of nodes. Similar to PageRank, but works only of the largest eigen vector of the adjacency matrix so does not converge in the same way and tends to more strongly favor high degree nodes.  It can be more approriate in certain use cases, particularly those with undirected relationships.
* *Article Rank*: A variant pf pageRank which assumes that relationships originating from low-degree nodes have a higher influence than relationships from high-degree nodes.

A full list of centrality algorithms across all project tiers can be found in the https://neo4j.com/docs/graph-data-science/current/algorithms/centrality/:[GDS documentation].


=== Check your understanding


[.summary]
== Summary
In this lesson you learned about Centrality in GDS.  We went over common use cases for degree centrality and how to run some of the most frequent centrality algorithms in GDS.  In the next lesson we will cover the algorithm category of path finding.