= Working with Lists and Maps
:type: quiz
:sandbox: true

//[.video]
//video::ImKvXY72SA0[youtube,width=560,height=315]

[.transcript]
== Aggregation in Cypher

In this lesson you will learn how aggregation works in a Cypher query and how to work with properties that contain multiple values.

When you aggregate in a Cypher query, the query must process all patterns in the `MATCH` clause to complete the aggregation to  return results.

Here is an example:

[source,cypher]
----
MATCH (a:Person)-[:ACTED_IN]->(m:Movie)<-[:DIRECTED]-(d:Person)
RETURN a.name, d.name, count(m) ORDER BY count(m) DESC
----

The query engine processes all nodes and relationships in the pattern so that it could perform a count of all movies for a particular actor/director pair in the graph.
Then, it returns the results grouped by the name of the director and actor pair.

For this query, Clint Eastwood acted in and directed 20 movies, Woody Allen acted in and directed 19 movies, and Adam Sandler acted in the same 8 movies that Dennis Dugan directed.
Aggregation in Cypher is different from aggregation in SQL.
In Cypher, you need not specify a grouping key.
As soon as an aggregation function, `count()` is used, all non-aggregated result columns become grouping keys.

The grouping is implicitly done, based upon the fields in the `RETURN` clause.

[NOTE]
--
If you specify `count(n)`, the graph engine calculates the number of non-null occurrences of _n_.
If you specify `count(*)`, the graph engine calculates the number of rows retrieved, including those with null values.
--

== Lists in the graph

You have already seen data in the graph that is stored as lists:

[source,cypher]
----
MATCH (m:Movie)
RETURN m.languages AS Languages, m.countries AS Countries LIMIT 10
----

For this query, notice that these lists are shown in square brackets and each element of the list is separated by a comma.
These lists contain a set of strings.

=== Using collect() to create a list

Cypher has a built-in function, `collect()` that enables you to aggregate values into a list.
The value can be a property value, a node, a relationship, or a path.

[source,cypher]
----
MATCH (a:Person)-[:ACTED_IN]->(m:Movie)<-[:DIRECTED]-(d:Person)
RETURN a.name, d.name, count(m), collect(m.title) as Movies  ORDER BY count(m) DESC
----

This query returns a list of movie titles associated with each actor/director name pair.
The rows are presented such that the pairs with the most number of titles are returned first.

=== Eliminating duplication in lists

Just as you have learned to eliminate duplication in data returned using `DISTINCT`, you can also eliminate duplication in a list.

Here is a query that has duplication in its result:

[source,cypher]
----
MATCH (a:Person)-[:ACTED_IN]->(m:Movie)
WHERE m.year = 1920
RETURN  collect(m.title) AS Movies, collect( a.name) AS Actors
----

When this query executes, it returns duplicate titles in the Movies list.

You can eliminate duplication in the list as follows:

[source,cypher]
----
MATCH (a:Person)-[:ACTED_IN]->(m:Movie)
WHERE m.year = 1920
RETURN  collect( DISTINCT m.title) AS Movies, collect( a.name) AS Actors
----

=== Collecting nodes

Rather than collecting the values of the _title_ properties for  movies, you can collect the nodes.
For this simple query, it is the same as returning _m_, but for more complex queries, you will learn later in this course that collecting nodes and using them for a later step of the query is useful.

[source,Cypher,role=noplay]
----
MATCH (p:Person)-[:ACTED_IN]->(m:Movie)
WHERE p.name ='Tom Cruise'
RETURN collect(m) AS `movies for Tom Cruise`
----

This query returns a list of all Movie nodes for Tom Cruise. In table view, you can see that the returned data is a list.

If you were to replace `RETURN collect(m) AS `movies for Tom Cruise`` clause with `RETURN m`, you would see that in table view the returned data is no longer a list.

=== Accessing elements of a list

You can access particular elements of the list using the `[index-value]` notation where a list begins with index 0.

In this example we return the first cast member for each movie.

[source,cypher]
----
MATCH (a:Person)-[:ACTED_IN]->(m:Movie)
RETURN m.title, collect(a.name)[0] as `A cast member`,
       size(collect(a.name)) as castSize
----

===  `count()` versus `size()`

There are more aggregating functions such as `min()` or `max()` that you can also use in your queries.
These are described in the link:https://neo4j.com/docs/cypher-manual/current/functions/aggregating/[Aggregating Functions section of the Neo4j Cypher Manual^].

You can either use `count()` to count the number of rows, or alternatively, you can return the size of the collected results.
The `size()` function returns the number of elements in a list.

Here we use `size()` to return the number of elements in the list.

[source,cypher]
----
MATCH (actor:Person)-[:ACTED_IN]->(m:Movie)<-[:DIRECTED]-(director:Person)
RETURN actor.name, director.name, size(collect(m)) AS collaborations,
       collect(m.title) AS movies
----

[NOTE]
--
As you gain more experience with query tuning, you will learn that `count()` is more efficient because it gets its values from the internal count store of the graph.
--

If we profile this query (run it at least twice):

[source,cypher]
----
PROFILE MATCH (g:Genre)<-[:IN_GENRE]-(m)
RETURN g.name AS Genre, size(collect(m)) as `Num Movies` ORDER BY size(collect(m)) DESC
----

It returns the same results as this query. This query uses count():

[source,cypher]
----
PROFILE MATCH (g:Genre)<-[:IN_GENRE]-(m:Movie)
RETURN g.name AS Genre, count(m) as `Num Movies` ORDER BY count(m) DESC
----

The first query requires more db hits than the second query.

And you can answer the question, What genre of movies is the highest and how many movies are in that genre?

[source,cypher]
----
PROFILE MATCH (g:Genre)<-[:IN_GENRE]-(m:Movie)
RETURN g.name AS Genre, count(m) as `Num Movies` ORDER BY count(m) DESC LIMIT 1
----

=== Pattern comprehension

Pattern comprehension is a very powerful way to create lists.
It behaves like an `OPTIONAL MATCH` combined with `collect()`.

Here is an example:

[source,cypher]
----
MATCH (m:Movie)
WHERE m.year = 2015
RETURN m.title,
[(dir:Person)-[:DIRECTED]->(m) | dir.name] AS Directors,
[(actor:Person)-[:ACTED_IN]->(m) | actor.name] AS Actors
----

Notice that for pattern comprehension we specify the list with the square braces to include the pattern followed by the pipe character to then specify what value will be placed in the list from the pattern.

`[<pattern> | value]`

This query returns the movies released in 2015.
Each row contains the title of the movie, the list of director names, and the list of actor names.

Here is another example of using pattern comprehension to create a list where we specify a filter for the pattern.

[source,cypher]
----
MATCH (a:Person {name: 'Tom Hanks'})
RETURN [(a)-->(b:Movie) WHERE b.title CONTAINS 'Toy' | b.title + ": " + b.year] AS Movies
----

For this pattern comprehension, the title of the movie is concatenated with the year of the movie as a value to add as an element of the list returned.


== Working with maps

A Cypher `map` is list of key/value pairs where each element of the list is of the format 'key': value. For example, a map of months and the number of days per month could be:

`{Jan: 31, Feb: 28, Mar: 31, Apr: 30 , May: 31, Jun: 30 , Jul: 31, Aug: 31, Sep: 30, Oct: 31, Nov: 30, Dec: 31}`

Using this map, we can return the value for one of its elements:

[source,cypher]
----
RETURN {Jan: 31, Feb: 28, Mar: 31, Apr: 30 , May: 31, Jun: 30 ,
       Jul: 31, Aug: 31, Sep: 30, Oct: 31, Nov: 30, Dec: 31}['Feb'] AS DaysInFeb
----

Here we use the key, `'Feb'` to access its value.

A node in the graph, when returned in Neo4j Browser is a map, when displayed as table rows. For example, a Movie node:

[source,cypher]
----
MATCH (m:Movie)
RETURN m LIMIT 1
----

=== Map projections

Map projections are when you can use retrieved nodes to create or return some of the information in the nodes.
A _Movie_ node can have the properties _title_, _released_, and _tagline_.
Suppose we want to return the _Movie_ node information, but without the _tagline_ property?
You can do so as follows using map projections:

[source,cypher]
----
MATCH (m:Movie)
WHERE m.title CONTAINS 'Matrix'
RETURN m { .title, .released } AS movie
----

== Check your understanding

include::questions/1-complete-query.adoc[leveloffset=+1]
include::questions/2-select-answer.adoc[leveloffset=+1]

[.summary]
== Summary

In this lesson, how to create lists, how aggregation works in Cypher, counting, and using maps.
In the next challenge, you will write a query to create a list.
