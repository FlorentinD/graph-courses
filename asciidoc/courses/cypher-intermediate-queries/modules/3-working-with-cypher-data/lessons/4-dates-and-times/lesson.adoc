= Working with Dates and Times
:type: quiz
:sandbox: true

//[.video]
//video::ImKvXY72SA0[youtube,width=560,height=315]

//[.transcript]
== Date and Time data in Cypher

Cypher has these several basic formats for storing date and time data.

[source,cypher]
----
RETURN date(), datetime(), time(), timestamp()
----

There are a number of other types of data such as _Time_, _LocalTime_, _LocalDateTime_, and _Duration_ which are described in the link:https://neo4j.com/docs/cypher-manual/current/functions/temporal/[Temporal Functions section of the Neo4j Cypher Manual^].

For training purposes we will create some date/time properties. Execute the following code to create a node in the graph containing these types:

[source,cypher]
----
MERGE (x:Test {id: 1})
SET x.date = date(),
    x.datetime = datetime(),
    x.time = time(),
    x.timestamp = timestamp()
RETURN x
----

Next, execute this code that will show the types of the properties in the graph:

[source,cypher]
----
CALL apoc.meta.nodeTypeProperties()
----

Notice the types for properties stored in the graph. The graph has Person data of type Date and you have added properties of type DateTime and Time.
Notice also that the timestamp is stored in the graph as a Long.

=== Extracting components of a date or datetime

You can access the components of a date or datetime property:

[source,cypher]
----
MATCH (x:Test)
RETURN x.date.day, x.date.year, x.datetime.year, x.datetime.hour,
       x.datetime.minute
----

=== Extracting components of a timestamp

Working with `timestamp()` is different as its value is a long integer that represents time.
The value of `datetime().epochmillis` is the same as `timestamp()`.
To extract a month, year, or time from a timestamp, you would do the following:

[source,cypher]
----
MATCH (x:Test)
RETURN datetime({epochmillis:x.timestamp}).day,
       datetime({epochmillis:x.timestamp}).year,
       datetime({epochmillis:x.timestamp}).month
----

=== Setting date values

You can use a string to set a value for a date:

[source,cypher]
----
MATCH (x:Test)
SET x.date1 = date('2022-01-01'),
    x.date2 = date('2022-01-15')
RETURN x
----

=== Setting datetime values

You can use a string to set a value for a datetime:

[source,cypher]
----
MATCH (x:Test)
SET x.datetime1 = datetime('2022-01-04T10:05:20'),
    x.datetime2 = datetime('2022-04-09T18:33:05')
RETURN x
----

== Working with durations

A duration is used to determine the difference between two date/datetime values or to add or subtract a duration to a value.

This code returns the duration between date1 and date2 in the graph:

[source,cypher]
----
MATCH (x:Test)
RETURN duration.between(x.date1,x.date2)
----

It returns a duration value that represents the days and months and times between the two values.
In this case, the duration between date1 and date2 is 14 days.

We can return the duration in days between two datetime values:

[source,cypher]
----
MATCH (x:Test)
RETURN duration.inDays(x.datetime1,x.datetime2).days
----

We can add a duration of 6 months:

[source,cypher]
----
MATCH (x:Test)
RETURN x.date1 + duration({months: 6})
----

== Using APOC to format dates and times

The APOC library has many useful functions for working with all types of data.

Here is one way you can use APOC to format a date:

[source,cypher]
----
MATCH (x:Test)
RETURN x.datetime as Datetime, apoc.temporal.format( x.datetime, 'HH:mm:ss.SSSS') AS `Formatted Datetime`
----

Here is another example:

[source,cypher]
----
MATCH (x:Test)
RETURN apoc.date.toISO8601(x.datetime.epochMillis, "ms") AS iso8601
----

== Check your understanding

include::questions/1-complete-query.adoc[leveloffset=+1]
include::questions/2-select-answer.adoc[leveloffset=+1]

[.summary]
== Summary

In this lesson, how to work with date, datetime, and timestamp data in Cypher.

In the next challenge, you will write queries that work with these data types.
