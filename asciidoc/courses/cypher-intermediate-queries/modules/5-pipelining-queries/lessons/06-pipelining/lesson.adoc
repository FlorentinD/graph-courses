= Pipelining Queries
:type: quiz
:sandbox: true

//[.video]
//video::ImKvXY72SA0[youtube,width=560,height=315]

//[.transcript]
== What does pipelining mean?

You have learned that you can use the `WITH` clause to redefine the scope for the query:

[source,cypher]
----
WITH  'Tom Hanks' AS theActor
MATCH (p:Person)-[:ACTED_IN]->(m:Movie)
WHERE p.name = theActor
WITH m  LIMIT 5
// possibly do more with the five m nodes
RETURN m.title AS Movies
----

This query limits the query to retrieve five nodes.

You can do something with the five nodes by adding another `MATCH` clause:

[source,cypher]
----
WITH  'Tom Hanks' AS theActor
MATCH (p:Person)-[:ACTED_IN]->(m:Movie)
WHERE p.name = theActor
WITH m  LIMIT 5
MATCH (d:Person)-[:DIRECTED]->(m)
RETURN d.name as Director, m.title AS Movies
----

The five Movie nodes are used for the second `MATCH` clause and five rows are returned.
The `WITH` clause enables us to pipeline the results of the first query into the second query.
With this simple query, m need not be redefined or scoped.
Next you will learn how pipelining is really useful.

=== Using WITH for aggregation

A very common use of `WITH` is to aggregate data so that intermediate results can be used to create the final returned data or to pass the intermediate results to the next part of the query.

Here is an example:

[source,cypher]
----
MATCH (:Movie {title: 'Toy Story'})-[:IN_GENRE]->(g:Genre)<-[:IN_GENRE]-(m)
WHERE m.imdbRating IS NOT NULL
WITH g.name as Genre, count(m) as moviesInCommon, sum(m.imdbRating) as total
RETURN Genre, moviesInCommon as `Movies in Common`,  total/moviesInCommon as Score ORDER by Score DESC
----

In this query we are counting the number of movies that share the same Genre node.
We use count() to count the number of rows and sum() to total the imdbRating for each movie for the Genre.
These values are calculated as part of the aggregation and then used to return the data.
In the `WITH` clause we pass on only the values we need to return a row for the Genre.

Here is another example that shows aggregation and pipelining:

[source,cypher]
----
MATCH (u:User {name: "Misty Williams"})-[r:RATED]->(:Movie)
WITH u, avg(r.rating) AS average
MATCH (u)-[r:RATED]->(m:Movie)
WHERE r.rating > average
RETURN average , m.title AS Movie, r.rating as Rating ORDER BY Rating DESC
----

For this query, we first calculate the average rating for all movies that Misty Williams rated.
Then we use this calcuated value, average as a test for the second `MATCH`.

=== Using WITH for collecting

Another common use for the `WITH` clause is to collect results into a list that will be returned:

[source,cypher]
----
MATCH (m:Movie)--(a:Actor)
WHERE m.title CONTAINS 'New York'
WITH m, collect (a.name) AS actors, count(*) AS numActors
RETURN m.title AS `Movie title`, actors ORDER BY numActors DESC
----

This query collects the names of actors who acted in the movies containing the string 'New York'.
This aggregation collects the names and totals the number of actors.

Here is another example where we perform a 2-step aggregation for collecting a list of maps:

[source,cypher]
----
MATCH (m:Movie)<-[:ACTED_IN]-(a:Actor)
WHERE m.title CONTAINS 'New York'
WITH m, collect (a.name) AS actors, count(*) AS numActors ORDER BY numActors DESC
RETURN collect(m { .title, actors, numActors }) AS movies
----


=== Using LIMIT early

A best practice is to execute queries that minimize the number of rows processed in the query.
One way to do that is to limit early in the query.
This also helps in reducing the number of properties loaded from the database too early.

For example, this query limits during the aggregation:

[source,cypher]
----
MATCH (p:Person)-[:ACTED_IN]->(m:Movie)
WHERE p.born.year = 1980
WITH p, collect(m.title) AS Movies LIMIT 3
RETURN p.name as Actor, Movies
----

A better way to do this query is to limit early.
This query will perform slightly better because LIMIT is moved up in the query:

[source,cypher]
----
PROFILE MATCH (p:Actor)
WHERE p.born.year = 1980
WITH p  LIMIT 3
MATCH (p)-[:ACTED_IN]->(m:Movie)
WITH p, collect(m.title) AS Movies
RETURN p.name AS Actor,  Movies
----

=== Use DISTINCT when necessary

Here is an example of a query that retrieves three actors, then collects the names of the genres for the movies that actor acted in.

[source,cypher]
----
MATCH (p:Actor)
WHERE p.born.year = 1980
WITH p  LIMIT 3
MATCH (p)-[:ACTED_IN]->(m:Movie)-[:IN_GENRE]->(g:Genre)
WITH p, collect(g.name) AS Genres
RETURN p.name as Actor, Genres
----

Notice that for this query, the collected genre names are repeated.
You would want to ensure that they are not duplicated as follows:

[source,cypher]
----
MATCH (p:Actor)
WHERE p.born.year = 1980
WITH p  LIMIT 3
MATCH (p)-[:ACTED_IN]->(m:Movie)-[:IN_GENRE]->(g:Genre)
WITH p, collect(DISTINCT g.name) AS Genres
RETURN p.name as Actor, Genres
----

== Check your understanding

include::questions/1-complete-query.adoc[leveloffset=+1]
include::questions/2-select-answer.adoc[leveloffset=+1]

[.summary]
== Summary

In this lesson, you learned how `WITH` is used to create intermediate results that can be used for pipelining queries.

In the next challenge, you will write a query that uses `WITH` to provide intermediate results for further query processing.

