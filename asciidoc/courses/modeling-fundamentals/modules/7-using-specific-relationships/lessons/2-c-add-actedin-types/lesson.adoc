= Specializing ACTED_IN and DIRECTED Relationships
:type: challenge
:order: 2
:sandbox: true

In this Challenge, you will turn the instance model into the following:

image::images/instance-specialize-acted-directed.png[Specialized ACTED_IN and DIRECTED relationships,width=400,align=center]

where we specialize the ACTED_IN and DIRECTED relationships.

This Challenge has 4 steps.

*Step 1:* Execute this code to refactor the ACTED_IN relationships in the graph:

[source,cypher]
----
MATCH (n:Actor)-[r:ACTED_IN]->(m:Movie)
CALL apoc.merge.relationship(n,
                              'ACTED_IN_' + left(m.released,4),
                              {},
                              {},
                              m ,
                              {}) YIELD rel
RETURN COUNT(*) AS `Number of relationships merged`
----

It should create 5 relationships.

With this refactoring, we can now confirm that our rewritten query works for the use case:

12: What movies did an actor act in for a particular year?

*Step 2:* Run this query:

[source,cypher]
----
MATCH (p:Actor)-[:ACTED_IN_1995]-(m:Movie)
WHERE p.name = 'Tom Hanks'
RETURN m.title AS Movie
----

It should return the movie, Apollo 13.

*Step 3:* Next, modify the code you executed in Step 1 to refactor the graph so that all DIRECTED relationships will be specialized.
Run the refactoring code.

It should create 2 relationships.

With this refactoring and the previous refactoring, we can now confirm that our rewritten query works for the use case:

12: What movies did an actor act in for a particular year?

*Step 4:* Run this query:

[source,cypher]
----
MATCH (p:Person)-[:ACTED_IN_1995|DIRECTED_1995]-()
RETURN p.name as `Actor or Director`
----

It should return Tom Hanks and Martin Scorsese.

include::./questions/verify.adoc[leveloffset=+1]

[.summary]
== Summary

In this Challenge, you demonstrated that you can refactor the graph to specialize  the ACTED_IN and DIRECTED relationships.

In the next Challenge, you will refactor the _RATED_ relationship.
