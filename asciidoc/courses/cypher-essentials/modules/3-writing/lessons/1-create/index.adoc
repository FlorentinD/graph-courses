= Creating Data
:order: 1

In this lesson you will learn how to write Cypher statements to create nodes and relationships in the graph.

Using the Movies data model, you will create and execute Cypher statements to create Actors and Movies in our graph.

We use the `CREATE` keyword to create a _pattern_ in the database.

After the `CREATE` keyword, we specify the pattern that we want to create.
Usually this will be a single node or a relationship between two nodes.

Suppose we want to create a node to represent _Michael Cain_.
Run this Cypher code to create the node.

[source,cypher]
----
CREATE (p:Person {name: 'Michael Cain'})
----

It creates a single node in the graph.

Verify that the node was created.

[source,cypher]
----
MATCH (p:Person {name: 'Michael Cain'})
RETURN p
----

// browser::MATCH (n) RETURN count(n)[]

== Multiple Statements

We can also chain multiple `CREATE` statements together within a single Cypher code block.

[source,cypher]
----
CREATE (p:Person {name: 'Katie Holmes'})
CREATE (m:Movie {title: 'The Dark Knight'})
CREATE (p)-[:ACTED_IN]->(m)
RETURN p, m
----

This code creates two nodes and a relationship between them.
Because we have specified the variables `p` and `m`, we can use them in the code to create the relationship between the two nodes.

== Mix and Match

`CREATE` statements can be combined with other statements.
For example, if the _Person_ and _Movie_ nodes both already exist, we can find them using a `MATCH` statement before creating the relationship between them.

[source,cypher]
----
MATCH (p:Person {name: 'Michael Cain'})
MATCH (m:Movie {title: 'The Dark Knight'})
CREATE (p)-[:ACTED_IN]->(m)
RETURN p, m
----

Thic code creates one relationship in the graph.

It is good practice to use the `MATCH` statement first to find any nodes in the database before creating the relationships.

== Setting properties for a node or relationship

There are two ways that you can set a property for a node or relationship.

=== 1. Inline as part of the `CREATE` clause

As in the queries above, we have set the _name_ property for the _Person_ node inline.
We can do the same thing when creating a relationship by specifying the properties within braces `{` and `}`.

[source,cypher]
----
MATCH (p:Person {name: 'Michael Cain'})
CREATE (m:Movie {title: 'Batman Begins'})
CREATE (p)-[:ACTED_IN {roles: ['Alfred Penny']}]->(m)
RETURN p,m
----

In this code, we have created the _ACTED_IN_ relationship between the _Michael Cain_ node and the newly-created _Batman Begins_ node. And we set the _roles_ property for this relationship to an array of values - containing one value, _Alfred Penny_.
Notice that for inline property setting, we use the JSON-style of adding the property key/value pairs in braces `{ .. }`.

=== 2. Using the `SET` keyword for a variable

We also have the option to use the `SET` keyword for setting a property value.
In the context of particular `CREATE`  or `MATCH` statement, you can set property values.

[source,cypher]
----
MATCH (p:Person)-[r:ACTED_IN]->(m:Movie)
WHERE p.name = 'Michael Cain' AND m.title = 'The Dark Knight'
SET r.roles = ['Alfred Penny']
RETURN p, r, m
----

Here we use the variable `r` to reference the relationship that is traversed. After it is retrieved, we set the _roles_ property.

== Check your understanding

include::questions/1-create-keyword.adoc[]

include::questions/2-set-properties.adoc[]

