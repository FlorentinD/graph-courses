= Deleting Data
:order: 6

In a Neo4j database you can delete:

* nodes
* relationships
* properties

To delete any data in the database, you must first retrieve it, then you can delete it.

== Deleting a node

Suppose you have created a _Person_ node for _Jane Doe_.

Run this Cypher code to create the node:

[source,Cypher]
----
CREATE (p:Person {name: 'Jane Doe'})
----

You delete this node as follows where you first retrieve the node. Then with a reference to the node you can delete it.

[source,Cypher]
----
MATCH (p:Person)
WHERE p.name = 'Jane Doe'
DELETE p
----

== Deleting a relationship

Suppose we had our _Jane Doe_ node again where she was added as an actor in the movie, _The Matrix_.
Run this code to create the node and the relationship.

[source,Cypher]
----
MATCH (m:Movie {title: 'The Matrix'})
CREATE (p:Person {name: 'Jane Doe'})
MERGE (p)-[:ACTED_IN]->(m)
----

This code creates one node and the relationship from _Jane Doe_ to the _The Matrix_.

To leave the _Jane Doe_ node in the graph, but remove the relationship we retrieve the relationship and delete it.

[source,Cypher]
----
MATCH (p:Person {name: 'Jane Doe'})-[r:ACTED_IN]->(m:Movie {title: 'The Matrix'})
DELETE r
----

Run this Cypher code that recreates the relationship:

[source,Cypher]
----
MATCH (p:Person {name: 'Jane Doe'})
MATCH (m:Movie {title: 'The Matrix'})
MERGE (p)-[:ACTED_IN]->(m)
----

If we attempt to delete the _Jane Doe_ node, we will receive an error because it has relationships in the graph.

Try running this Cypher code:

[source,Cypher]
----
MATCH (p:Person {name: 'Jane Doe'})
DELETE p
----

You should receive an error. Neo4j prevents orphaned relationships in the graph.


== Deleting a node and its relationships

Neo4j provides a feature where you cannot delete a node if it has incoming or outgoing relationships.
This prevents the graph from having orphaned relationships.

You can delete the node and any of its relationships using `DETACH DELETE`.

[source,Cypher]
----
MATCH (p:Person {name: 'Jane Doe'})
DETACH DELETE p
----

This code deletes the relationship and the _Person_ node.

== Deleting a property

There are two ways that you can delete a property for a node, using `REMOVE` and using `SET`.

=== Using `REMOVE` to delete a property

Suppose, we want to remove the _tagline_ for the movie, _The Birdcage_.

In this code, we find the node and remove its property.

[source,Cypher]
----
MATCH (m:Movie {title: 'The Birdcage'})
REMOVE m.tagline
RETURN m
----

In the returned node, you should no longer see the _tagline_ property.

=== Using `SET` to delete a property

Suppose, we again want to remove the _tagline_ for the movie, _The Birdcage_.

Run this Cypher code to find the node and add the _tagline_ back in the node:

[source,Cypher]
----
MATCH (m:Movie {title: 'The Birdcage'})
SET m.tagline = 'Come as you are'
RETURN m
----

In the returned node, you should see the _tagline_ property again.

Here is another way to remove the property using `SET`.

[source,Cypher]
----
MATCH (m:Movie {title: 'The Birdcage'})
SET m.tagline=null
RETURN m
----

In the returned node, you should no longer see the _tagline_ property.


== Check your understanding

include::questions/1-complete-code.adoc[]

include::questions/2-delete-property.adoc[]


//input::read[type=button,class=btn,value=Mark as Read]